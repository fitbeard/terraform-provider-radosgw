---
subcategory: "IAM (Identity & Access Management)"
page_title: "RadosGW: radosgw_iam_quota"
description: |-
  Manages user-level quotas in RadosGW. This resource configures storage and object limits for a user.
  Important: This resource manages quotas at the user level, not individual bucket quotas. There are two types:
  
  User quota (type = "user"): Sets the total storage limit across ALL buckets owned by the user. When exceeded, the user cannot store more data in any of their buckets.
  Bucket quota (type = "bucket"): Sets a per-bucket limit that applies to EACH bucket owned by the user. Every bucket the user owns will have this same quota applied.
  Upon deletion, the quota is disabled (not removed, as quotas are properties of users).
  Note: Account-level quotas are not yet supported by the go-ceph library. Only user-level quotas are currently available.
---

# radosgw_iam_quota

Manages user-level quotas in RadosGW. This resource configures storage and object limits for a user.

**Important:** This resource manages quotas at the **user level**, not individual bucket quotas. There are two types:

- **User quota** (`type = "user"`): Sets the total storage limit across ALL buckets owned by the user. When exceeded, the user cannot store more data in any of their buckets.

- **Bucket quota** (`type = "bucket"`): Sets a per-bucket limit that applies to EACH bucket owned by the user. Every bucket the user owns will have this same quota applied.

Upon deletion, the quota is disabled (not removed, as quotas are properties of users).

**Note:** Account-level quotas are not yet supported by the go-ceph library. Only user-level quotas are currently available.

## Example Usage

```terraform
# User quota - limits total storage across ALL buckets owned by the user
resource "radosgw_iam_quota" "user_quota" {
  user_id     = radosgw_iam_user.example.user_id
  type        = "user"
  enabled     = true
  max_size    = 10737418240 # 10 GB in bytes
  max_objects = 10000
}

# Bucket quota - per-bucket limit applied to EACH bucket the user owns
resource "radosgw_iam_quota" "bucket_quota" {
  user_id     = radosgw_iam_user.example.user_id
  type        = "bucket"
  enabled     = true
  max_size    = 5368709120 # 5 GB in bytes
  max_objects = 5000
}

# Disabled quota (unlimited)
resource "radosgw_iam_quota" "unlimited" {
  user_id     = radosgw_iam_user.example.user_id
  type        = "user"
  enabled     = false
  max_size    = -1 # unlimited
  max_objects = -1 # unlimited
}

# Reference user resource
resource "radosgw_iam_user" "example" {
  user_id      = "quota-example-user"
  display_name = "Quota Example User"
}
```

<!-- schema generated by tfplugindocs -->

## Argument Reference

The following arguments are supported:


* `type` - (Required) The quota type:
  - `user`: Total quota across all user's buckets combined
  - `bucket`: Per-bucket quota applied to each bucket the user owns
* `user_id` - (Required) The user ID to configure quotas for.


* `enabled` - (Optional) Whether the quota is enabled. Default: `true`.
* `max_objects` - (Optional) Maximum number of objects. Use `-1` for unlimited. Default: `-1`.
* `max_size` - (Optional) Maximum size in bytes. Use `-1` for unlimited. Default: `-1`.


## Attributes Reference

The following attributes are exported:

* `type` - See Argument Reference above.
* `user_id` - See Argument Reference above.
* `enabled` - See Argument Reference above.
* `max_objects` - See Argument Reference above.
* `max_size` - See Argument Reference above.
## Import

Import is supported using the following syntax:

```shell
# Import a user quota (total quota across all user's buckets)
# Format: user_id:type (type is "user" or "bucket")
terraform import radosgw_iam_quota.user_quota example-user:user

# Import a bucket quota (per-bucket quota for all user's buckets)
terraform import radosgw_iam_quota.bucket_quota example-user:bucket
```
