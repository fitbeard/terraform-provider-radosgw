---
subcategory: "S3 (Simple Storage)"
page_title: "RadosGW: radosgw_s3_bucket_policy"
description: |-
  Attaches a policy to an S3 bucket in RadosGW. Only one policy can be attached to a bucket at a time.
  Bucket policies provide access control for S3 buckets and their objects. They use the same policy language as IAM policies
  but are attached directly to buckets rather than users or roles.
  ~> Note: Bucket policies in RadosGW support a subset of Amazon S3 policy features. See the
  Ceph RadosGW Bucket Policies documentation https://docs.ceph.com/en/latest/radosgw/bucketpolicy/ for supported actions and conditions.
  ~> Important: Destroying this resource will delete the bucket policy. The bucket itself will remain.
---

# radosgw_s3_bucket_policy

Attaches a policy to an S3 bucket in RadosGW. Only one policy can be attached to a bucket at a time.

Bucket policies provide access control for S3 buckets and their objects. They use the same policy language as IAM policies
but are attached directly to buckets rather than users or roles.

~> **Note:** Bucket policies in RadosGW support a subset of Amazon S3 policy features. See the
[Ceph RadosGW Bucket Policies documentation](https://docs.ceph.com/en/latest/radosgw/bucketpolicy/) for supported actions and conditions.

~> **Important:** Destroying this resource will delete the bucket policy. The bucket itself will remain.

## Example Usage

```terraform
# Basic bucket policy using jsonencode
resource "radosgw_s3_bucket" "example" {
  bucket = "my-example-bucket"
}

resource "radosgw_s3_bucket_policy" "example" {
  bucket = radosgw_s3_bucket.example.bucket

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "PublicReadGetObject"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:GetObject"
        Resource  = "arn:aws:s3:::my-example-bucket/*"
      }
    ]
  })
}

# Bucket policy using the policy_document data source
resource "radosgw_s3_bucket" "data_bucket" {
  bucket = "my-data-bucket"
}

data "radosgw_iam_policy_document" "bucket_policy" {
  statement {
    sid    = "AllowUserAccess"
    effect = "Allow"

    principals {
      type        = "AWS"
      identifiers = ["arn:aws:iam:::user/myuser"]
    }

    actions = [
      "s3:GetObject",
      "s3:PutObject",
      "s3:DeleteObject",
    ]

    resources = [
      "arn:aws:s3:::my-data-bucket/*",
    ]
  }

  statement {
    sid    = "AllowListBucket"
    effect = "Allow"

    principals {
      type        = "AWS"
      identifiers = ["arn:aws:iam:::user/myuser"]
    }

    actions = [
      "s3:ListBucket",
    ]

    resources = [
      "arn:aws:s3:::my-data-bucket",
    ]
  }
}

resource "radosgw_s3_bucket_policy" "data_bucket" {
  bucket = radosgw_s3_bucket.data_bucket.bucket
  policy = data.radosgw_iam_policy_document.bucket_policy.json
}

# Deny policy example - deny all except specific user
resource "radosgw_s3_bucket" "restricted" {
  bucket = "restricted-bucket"
}

data "radosgw_iam_policy_document" "restricted" {
  statement {
    sid    = "DenyAllExceptAdmin"
    effect = "Deny"

    not_principals {
      type        = "AWS"
      identifiers = ["arn:aws:iam:::user/admin"]
    }

    actions = ["s3:*"]

    resources = [
      "arn:aws:s3:::restricted-bucket",
      "arn:aws:s3:::restricted-bucket/*",
    ]
  }
}

resource "radosgw_s3_bucket_policy" "restricted" {
  bucket = radosgw_s3_bucket.restricted.bucket
  policy = data.radosgw_iam_policy_document.restricted.json
}

# Policy with conditions
resource "radosgw_s3_bucket" "conditional" {
  bucket = "conditional-bucket"
}

data "radosgw_iam_policy_document" "conditional" {
  statement {
    sid    = "AllowFromSpecificIP"
    effect = "Allow"

    principals {
      type        = "*"
      identifiers = ["*"]
    }

    actions = [
      "s3:GetObject",
    ]

    resources = [
      "arn:aws:s3:::conditional-bucket/*",
    ]

    condition {
      test     = "IpAddress"
      variable = "aws:SourceIp"
      values   = ["192.168.1.0/24", "10.0.0.0/8"]
    }
  }
}

resource "radosgw_s3_bucket_policy" "conditional" {
  bucket = radosgw_s3_bucket.conditional.bucket
  policy = data.radosgw_iam_policy_document.conditional.json
}
```

<!-- schema generated by tfplugindocs -->

## Argument Reference

The following arguments are supported:


* `bucket` - (Required) The name of the bucket to which the policy will be applied.
* `policy` - (Required) The policy document in JSON format. Use `jsonencode()` or the `radosgw_iam_policy_document` data source to generate this.




## Attributes Reference

The following attributes are exported:

* `id` - The bucket name (used as the resource ID).
* `bucket` - See Argument Reference above.
* `policy` - See Argument Reference above.
## Import

Import is supported using the following syntax:

```shell
# Import a bucket policy by bucket name
terraform import radosgw_s3_bucket_policy.example "my-bucket-name"
```
